// noinspection UnnecessaryReturnStatementJS

import QtQuick 2.15

Item {
    property var utils: null

    readonly property string providerId: "lrclib"
    readonly property string endpoint: "https://lrclib.net"
    readonly property string url_search: endpoint + "/api/search"

    function fetchNormalizedLyrics(trackName, artistName, albumName) {
        if (!utils) {
            console.error("LRCLIB provider is missing utils");
            return Promise.resolve(null);
        }

        const cacheKey = utils.buildLyricsCacheKey(trackName, artistName, albumName);

        return search(trackName, artistName, albumName)
            .then(data => {
                if (data.length <= 0) {
                    console.debug("No results found for", trackName, artistName, albumName);
                    return null;
                }

                let text = null;
                for (let i = 0; i < data.length; i++) {
                    if (!data[i].syncedLyrics) {
                        continue;
                    }
                    text = data[i].syncedLyrics;
                    break;
                }

                if (!text) {
                    console.debug("No synced lyrics found in any result");
                    return null;
                }

                const lines = utils.parseSyncedLyrics(text);
                if (!lines || lines.length <= 0) {
                    return null;
                }

                return utils.createLyricsPayload(lines, providerId, cacheKey, "synced");
            });
    }

    function fetchLyrics(trackName, artistName, albumName) {
        return fetchNormalizedLyrics(trackName, artistName, albumName)
            .then(payload => payload ? payload.lines : null);
    }

    function search(trackName, artistName, albumName) {
        return searchByTrack(trackName, artistName, albumName)
            .then(data => {
                if (data.length > 0) {
                    return data;
                }
                return searchByString(trackName + " " + artistName)
                    .then(data2 => {
                        if (data2.length > 0) {
                            return data2;
                        }
                        return searchByString(trackName)
                            .then(data3 => {
                                if (data3.length > 0) {
                                    data3 = data3.filter(item => {
                                        return item.artistName.toLowerCase() === artistName.toLowerCase();
                                    });
                                    return data3;
                                }
                                return [];
                            });
                    });
            }).catch(error => {
                console.error("Search failed:", error);
                return [];
            });
    }

    function searchByTrack(trackName, artistName, albumName) {
        let url = url_search
            + "?track_name=" + encodeURIComponent(trackName)
            + "&artist_name=" + encodeURIComponent(artistName)
            + "&album_name=" + encodeURIComponent(albumName);

        return utils.fetch(url)
            .then(response => response.json());
    }

    function searchByString(query) {
        let url = url_search + "?q=" + encodeURIComponent(query);

        return utils.fetch(url)
            .then(response => response.json());
    }
}
